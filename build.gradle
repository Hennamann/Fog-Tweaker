buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

ext.configFile = file('build.properties')
ext.localFile = file('local.properties')
ext.config = parseConfig(configFile)

if (localFile.exists()) {
    ext.local = parseConfig(localFile)
}

version = "${config.version}"
group = "com.henrikstabell.fogworld" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "fogworld"

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // snapshot   YYYYMMDD   Snapshot are built nightly.
    // stable     #          Stables are built at the discretion of the MCP team.
    // official   MCVersion  Official field/method names from Mojang mapping files
    //
    // You must be aware of the Mojang license when using the 'official' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'official', version: "${config.mcVersion}"

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Currently, this location cannot be changed from the default.

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                fogworld {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                fogworld {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'fogworld', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                fogworld {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url 'https://www.cursemaven.com' } // Configured etcâ€¦
    // flatDir { dirs "libs" }
}

dependencies {
    // Specify the version of Minecraft to use. If this is any group other than 'net.minecraft', it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency, and its patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${config.mcVersion}-${config.forgeVersion}"

    // Biome Testing
    runtimeOnly fg.deobf("com.github.glitchfiend:TerraBlender-forge:${config.mcVersion}-${config.terraBlenderVersion}")
    runtimeOnly fg.deobf("curse.maven:biomes-o-plenty-220318:${config.bopId}")
    runtimeOnly fg.deobf("curse.maven:oh-the-biomes-youll-go-247560:${config.bygId}")
    runtimeOnly fg.deobf("curse.maven:natures-compass-252848:${config.naturesCompassId}")

    // For testing config compat.
    runtimeOnly fg.deobf("curse.maven:configured-457570:${config.configuredId}")
    runtimeOnly fg.deobf("curse.maven:catalogue-459701:${config.catalogueId}")

}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

def getManifestAttributes(String title) {
    return [
            "Specification-Title"     : "Fogworld",
            "Specification-Vendor"    : "Hennamann",
            "Specification-Version"   : "${config.version}",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : project.jar.archiveVersion,
            "Implementation-Vendor"   : "Hennamann",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ]
}

jar {
    manifest.attributes(getManifestAttributes("Fogworld"))
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    manifest.attributes(getManifestAttributes("Fogworld"))
    archiveClassifier.set("sources")
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    manifest.attributes(getManifestAttributes("Fogworld"))
    archiveClassifier.set("dev")
}

task exportJavadoc(type: Javadoc) {
    source = sourceSets.main.allSource
}

task apiJar(type: Jar) {
    from sourceSets.main.allSource
    manifest.attributes(getManifestAttributes("Fogworld"))
    from sourceSets.main.output

    include 'com/henrikstabell/fogworld/api/**/*'
    archiveClassifier.set("api")
}

task copyBuildXml(type: Copy) {
    from 'build.properties'
    into 'output'
}

jar.finalizedBy('reobfJar')

if (localFile.exists()) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId 'com.henrikstabell.fogworld'
                artifactId 'FogWorld-1.18.1'

                artifact jar
                artifact deobfJar
                artifact sourcesJar
                artifact apiJar
            }
        }
        repositories {
            maven {
                credentials {
                    username = "${local.userMaven}"
                    password = "${local.passMaven}"
                }
                url "https://maven.henrikstabell.com/mods"
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}